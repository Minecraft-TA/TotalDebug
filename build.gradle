buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven/' }
        maven { url = 'https://plugins.gradle.org/m2/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'com.anatawa12.forge', name: 'ForgeGradle', version: '1.2-1.0.+', changing: true
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
    }
}

apply plugin: 'idea'
apply plugin: 'forge'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'com.github.minecraft_ta'
version = '1.8.0'
archivesBaseName = 'TotalDebug'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    version = '1.7.10-10.13.4.1614-1.7.10'
    runDir = "run"
}

processResources {
    inputs.property "version", project.version

    setDuplicatesStrategy(DuplicatesStrategy.WARN)
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    maven {
        url "https://dvs1.progwml6.com/files/maven"
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url "https://packagecloud.io/tth05/repo/maven2"
    }
    maven {
        url "https://maven.ic2.player.to/"
        metadataSources {
            mavenPom()
            artifact()
        }
    }
    maven {
        name 'GTNH Maven'
        url "http://jenkins.usrv.eu:8081/nexus/content/groups/public/"
        allowInsecureProtocol(true)
    }
    mavenCentral()
}

dependencies {
    implementation "com.github.tth05:SCNet:d683b47"
    implementation 'io.github.classgraph:classgraph:4.8.146'
    implementation "org.bitbucket.mstrobel:procyon-compilertools:0.6.0"
    implementation "com.github.tth05:jindex:0.0.45"
    implementation "com.github.GTNewHorizons:NotEnoughItems:2.3.45-GTNH"
    implementation "net.industrial-craft:industrialcraft-2:2.2.828-experimental:dev"
    implementation "com.github.GTNewHorizons:GT5-Unofficial:5.09.41.145:dev"

    implementation group: 'fastutil', name: 'fastutil', version: '5.0.9'
    implementation('com.github.javaparser:javaparser-symbol-solver-core:3.24.0') {
        exclude group: "com.google.guava"
    }
}

shadowJar {
    classifier ''

    setDuplicatesStrategy(DuplicatesStrategy.WARN)
    minimize()
    relocate "com.github.javaparser", "com.github.minecraft_ta.totaldebug.shades.javaparser"
    relocate "io.github.classgraph", "com.github.minecraft_ta.totaldebug.shades.classgraph"
    relocate "nonapi.io.github.classgraph", "com.github.minecraft_ta.totaldebug.shades.nonapi"
    relocate "com.strobel", "com.github.minecraft_ta.totaldebug.shades.procyon"
    relocate "com.github.tth05.scnet", "com.github.minecraft_ta.totaldebug.shades.scnet"
    dependencies {
        exclude("**/module-info.class")
        exclude("**/maven/**")
        exclude(dependency {
            it.moduleGroup != 'io.github.classgraph' &&
                    it.moduleGroup != 'com.github.javaparser' &&
                    it.moduleGroup != 'org.bitbucket.mstrobel' &&
                    it.moduleGroup != 'com.github.tth05' &&
                    it.moduleGroup != 'fastutil'
        })
    }
}

reobf.dependsOn shadowJar


idea { module { inheritOutputDirs = true } }
