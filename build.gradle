buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
    }
}

apply plugin: 'idea'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'antlr'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'com.github.minecraft_ta'
version = '1.0'
archivesBaseName = 'TotalDebug'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: 'stable', version: '39-1.12'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                codedebug {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                codedebug {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'

    implementation "io.github.classgraph:classgraph:4.8.90"
    implementation "org.bitbucket.mstrobel:procyon-compilertools:0.5.36"

    implementation 'org.antlr:antlr4-runtime:4.7.2'
    antlr 'org.antlr:antlr4:4.7.2'
}

shadowJar {
    classifier ''

    relocate "org.antlr", "com.github.minecraft_ta.totaldebug.shades.antlr"
    relocate "io.github.classgraph", "com.github.minecraft_ta.totaldebug.shades.classgraph"
    relocate "com.strobel", "com.github.minecraft_ta.totaldebug.shades.procyon"
    dependencies {
        exclude(dependency {
            it.moduleGroup != 'io.github.classgraph' &&
                    it.moduleGroup != 'org.antlr' &&
                    it.name != 'net.minecraftforge:fernflower:402' &&
                    it.moduleGroup != 'org.bitbucket.mstrobel'
        })
    }
}

reobf {
    shadowJar {}
}

idea { module { inheritOutputDirs = true } }
